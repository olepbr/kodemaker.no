:title Property-based testing i Clojure
:author anders
:tech [:clojure :testing]
:published 2021-12-15

:blurb

Er du lei av Ã¥ skrive tester? Hva om jeg fortalte deg at det finnes
verktÃ¸y som genererer testene for deg? For godt til Ã¥ vÃ¦re sant?

:body

For noen Ã¥r tilbake dro flere kollegaer og jeg til en konferanse for faglig
pÃ¥fyll og inspirasjon. Et av foredragene bar tittelen "Don't write tests!
Generate them". Nysgjerrigheten ble umiddelbart pirret selv om dette hÃ¸rtes for
godt ut til Ã¥ vÃ¦re sant. Foredragsholderen proklamerte videre at han ved denne
formen for testing hadde avdekket dype, intrikate feil i alt fra
telefonsystemer, styringssystemer til biler, samt databaseimplementasjoner. Feil
som nÃ¦r sagt hadde vÃ¦rt umulig Ã¥ avdekke i form av tradisjonelle tester.

Foredragsholderen var John Hughes og er mannen bak
[QuickCheck](https://hackage.haskell.org/package/QuickCheck), det opprinnelige
verktÃ¸yet for Ã¥ skrive Property-Based tester.

Denne bloggposten er den fÃ¸rste i en serie av flere. I fÃ¸rste omgang vil jeg gi
deg en kort innfÃ¸ring i Property-Based Testing (PBT). Vi skal skrive et par
tester mot en triviell, velkjent funksjon og ta et aldri sÃ¥ lite dykk nedi
[test.check](https://github.com/clojure/test.check), et PBT-verktÃ¸y for
[Clojure](/clojure). Dersom Clojure er fremmed for deg anbefaler jeg
deg likevel fortsatt til Ã¥ lese videre. Anatomien og prinsippene bak PBT er
universelle pÃ¥ tvers av teknologi.

StÃ¸tte for PBT finnes for et bredt utvalg av sprÃ¥k. Et raskt sÃ¸k pÃ¥ nett vil
gi deg pekere til testverktÃ¸y for eksempelvis
[JavaScript](https://github.com/dubzzz/fast-check),
[Ruby](https://github.com/hayeah/rantly),
[Java](http://java.net/projects/quickcheck/pages/Home),
[Go](https://github.com/flyingmutant/rapid), [Erlang /
Elixir](https://github.com/pragdave/quixir) og
[Python](https://github.com/DRMacIver/hypothesis).

## Anatomi

Den _tradisjonelle_ mÃ¥ten Ã¥ skrive tester av en funksjon gÃ¥r som fÃ¸lger:

1. Vi formulerer et konkret eksempel pÃ¥ input til funksjonen.
2. Vi kaller pÃ¥ funksjonen med det konkrete eksempelet som input.
3. Vi verifiserer at resultatet stemmer med vÃ¥re forventninger.

En property-based test genererer testene for deg. Dette skjer ved at:

1. Vi formulerer en mer _generell_ beskrivelse av input ved hjelp av
   testverktÃ¸yet.
2. TestverktÃ¸yet genererer et _konkret og tilfeldig_ eksempel som passer
   beskrivelsen.
3. Vi kaller pÃ¥ funksjonen med det konkrete eksempelet som input.
4. Vi verifiserer at resultatet stemmer med vÃ¥re forventninger.

Ettersom det er testverktÃ¸yet, og ikke du, som genererer input til funksjonen
din vil testverktÃ¸yet kunne generere "uendelig" antall unike tester mot
funksjonen din. NÃ¥r jeg sier "funksjon", sÃ¥ mener jeg ikke en nÃ¸dvendigvis en
funksjon som er ["pure"](https://en.wikipedia.org/wiki/Pure_function). Tvert
imot; PBT er like, om ikke mer, nyttig til testing av metoder eller systemer med
[sideeffekter](https://en.wikipedia.org/wiki/Side_effect_(computer_science)). I
min erfaring er det gjerne nettopp der de fleste feilene oppstÃ¥r og gjerne ogsÃ¥
hvor de er mest vanskelig Ã¥ avdekke. "Funksjonen" har i sÃ¥ tilfelle ansvar for Ã¥
sette opp systemet i en tilstand som lar seg teste.

La oss konkretisere disse stegene i litt kode ved Ã¥ skrive en test for noe sÃ¥
trivielt som en funksjon som sorterer en liste av heltall.

## Formulere beskrivelse av input

TestverktÃ¸yet vÃ¥rt, `test.check`, tilbyr et bredt utvalg av funksjoner (kalt
generatorer) for Ã¥ beskrive input. La oss finne beskrivelsen av heltall. Jeg
bruker hjelpefunksjonen `sample` for Ã¥ trekke ut 20 konkrete eksempler fra
heltall-generatoren:

```clj
(ns sorter-heltall-test
  (:require [clojure.test.check.generators :as gen]))

(gen/sample gen/int 20)

=> (0 0 1 -2 -4 1 -3 7 8 8 -3 -2 7 13 0 11 -9 17 14 -18)
```

Sweet! Vi trenger dog Ã¥ beskrive en liste av heltall som input til
sorteringsfunksjonen vÃ¥r. Generatoren `gen/list` hjelper oss med dette. Den tar
en vilkÃ¥rlig generator som argument og genererer lister med elementer generert
fra denne:

```clj
(gen/sample (gen/list gen/int) 10)

=> (() (-1) (1) (-2 -3 -1) (0 3 1) (-1 -2 -1 1 -1) (1 -3 -1 6 0 -3) (2 -4) (-2) (-3 2 5 0 -5 3))
```

Flotters. Da var det fÃ¸rste steget i orden. Vi har nÃ¥ en generell beskrivelse av
input til funksjonen vÃ¥r: `(gen/list gen/int)`.

## Formidle beskrivelsen til testverktÃ¸yet

De neste par stegene er Ã¥ overlate denne beskrivelsen til testverktÃ¸yet (som
tross alt skal generere testene), samt kalle pÃ¥ sorteringsfunksjonen vÃ¥r.

```clj
(ns sorter-heltall-test
  (:require [clojure.test.check :as tc]
            [clojure.test.check.generators :as gen]
            [clojure.test.check.properties :as prop]))

(def sorter-heltall sort)

(tc/quick-check
 1000
 (prop/for-all
  [input (gen/list gen/int)]
  (let [output (sorter-heltall input)]
    ;; TODO: steg 4, verifisere output
    )))
```

Vi forteller `quick-check` at vi Ã¸nsker Ã¥ generere og kjÃ¸re `1000` tester.
Beskrivelsen av input gir vi til testverktÃ¸yets `for-all`, som i tur vil
generere det ene konkrete eksempel etter det andre. Hvert konkret eksempel blir
bundet til symbolet vi har navngitt `input`. Videre forventer `for-all` at vi
signaliserer at en test feiler ved Ã¥ enten returnere `false` eller kaste en
exception.

## Verifisering

Det siste og fjerde steget er uten tvil det vanskeligste i PBT. Hvordan skal vi
fÃ¥ verifisert output'n fra sorteringsfunksjonen vÃ¥r nÃ¥r vi ikke vet hva den har
fÃ¥tt som input? Ettersom vi har beskrevet input'n i generelle termer blir vi
pent nÃ¸dt til Ã¥ gjÃ¸re det samme i verifiseringssteget.

Hva er det sorteringsfunksjonen vÃ¥r _gjÃ¸r_? Hvilke _egenskaper_ (derav navnet
"property-based", forresten) har funksjonen vÃ¥r? Dette hÃ¸res ut som banale
spÃ¸rsmÃ¥l, men det er spÃ¸rsmÃ¥l som mÃ¥ besvares i kode. La oss bryte det ned.
Output fra sorteringsfunksjonen er:

1. en liste som inneholder (og kun inneholder) alle elementene som ble gitt som input
2. en liste hvis elementer er i stigende rekkefÃ¸lge

## En kjÃ¸rende test!

La oss komplettere testen vÃ¥r med det fjerde og siste steget. For Ã¥ lette
eventuell feilsÃ¸king velger jeg Ã¥ lage egne tester for hver egenskap.

```clj
(ns sorter-heltall-test
  (:require [clojure.test.check :as tc]
            [clojure.test.check.generators :as gen]
            [clojure.test.check.properties :as prop]))

(def sorter-heltall sort)


;; Egenskap 1: Inneholder (og kun inneholder) alle
;; elementene som ble gitt som input

(tc/quick-check
 1000
 (prop/for-all
  [input (gen/list gen/int)]
  (let [output (sorter-heltall input)]
    (= (frequencies input)
       (frequencies output)))))


;; Egenskap 2: Elementene er i stigende rekkefÃ¸lge

(tc/quick-check
 1000
 (prop/for-all
  [input (gen/list gen/int)]
  (let [output (sorter-heltall input)]
    (or (and (empty? input) (empty? output))
        (apply < output)))))
```

Vi bruker Clojures basale og innebygde funksjoner for Ã¥ implementere vÃ¥re
forventninger.

NÃ¥ er vi endelig klar til Ã¥ sette maskinen i arbeid og nyte synet av
mangfoldige tester suse avgÃ¥rde i blindende glimt av betryggende grÃ¸nnfarge!

ğŸ¥

```clj
...

(tc/quick-check
 1000
 (prop/for-all
  [input (gen/list gen/int)]
  (let [output (sorter-heltall input)]
    (= (frequencies input)
       (frequencies output)))))

=> {:num-tests 1000
    :pass? true
    :seed 1639440034364
    :time-elapsed-ms 45}

...
```

âœ… Herlig! 1000 grÃ¸nne tester generert og kjÃ¸rt pÃ¥ et blunk.

La oss teste egenskap #2:

```clj
(tc/quick-check
 1000
 (prop/for-all
  [input (gen/list gen/int)]
  (let [output (sorter-heltall input)]
    (or (and (empty? input) (empty? output))
        (apply < output)))))

=> {:fail [(7 -3 5 -7 5 -5 -2 -2)]
    :num-tests 9
    :pass? false
    :seed 1639435177764
    :shrunk {:depth 2
             :pass? false
             :smallest [(-2 -2)]}}

```

ğŸ˜± What!?

Etter 9 iterasjoner har testen vÃ¥r feilet. Funksjonen vÃ¥r ble fÃ´ret `(7 -3 5 -7
5 -5 -2 -2)`. TestverktÃ¸yet har dog, pÃ¥ nesten magisk vis, krympet input'n og
funnet ut at funksjonen vÃ¥r faktisk allerede feiler nÃ¥r den blir gitt `(-2 -2)`!

Jeg var litt slurvete i sjekken av egenskapen; med `(apply < output)` tar vi jo
ikke hÃ¸yde for at listen kan inneholde identiske elementer. Vi bytter `<` med
`<=` og fyrer opp pÃ¥ nytt.

```clj
{:result true, :pass? true, :num-tests 1000, :time-elapsed-ms 53, :seed 1639436350880}
```

SÃ¥Ã¥Ã¥Ã¥Ã¥Ã¥Ã¥Ã¥Ã¥nn! ğŸ¥³

## Krymping og tilfeldigheter

Dette med krympingen av input kan sikkert oppfattes som et nÃ¦rmest ubetydelig
bidrag i dette konkrete eksempelet. Ved testing av funksjoner med mer kompleks
logikk og mistenkelig mange parametre, feilende input som inneholder hundrevis
av elementer og du i tillegg ikke ser bunnen pÃ¥ nÃ¸stingen av hvert enkelt
element... da kan jeg love deg at denne krympingen er helt og fullstendig
uvurderlig!

En annen ting: Settet av testdata som genereres ved kjÃ¸ring av testen vÃ¥r vil
variere. Den er jo tross alt tilfeldig generert. Dette er noe av styrken til
property-based testing. Selv om vi i dissee testen har sagt vi kun Ã¸nsker Ã¥
utfÃ¸re 2000 tester vil de i stor grad vÃ¦re unike mellom hver testkjÃ¸ring. NÃ¥r
testen fÃ¸rst feiler kan det vÃ¦re fordelaktig Ã¥ kunne kjÃ¸re pÃ¥fÃ¸lgende tester med
tilsvarende generert input. Spesielt dersom du tester funksjoner som ikke er
pure! Testrapporten oppgir derfor hvilket `:seed` som er benyttet i genereringen
av input. Denne verdien kan du angi til `quick-check` for Ã¥ enkelt reprodusere
feilen.

Generatorene i `test.check` (og trolig tilsvarende konsepter i ditt fremtidige
favorittverktÃ¸y for PBT) er dog ikke helt tilfeldig. De har alle en formening om
relativ "stÃ¸rrelse" eller "kompleksitet" i bestanddelene som blir generert.
TestverktÃ¸yet vil stadig generere mer og mer komplekse permutasjoner av
beskrivelsen du har formidlet, bÃ¥de for Ã¥ tidlig kunne detektere elementÃ¦re feil
og gjÃ¸re en god jobb med Ã¥ krympe datasettet, men samtidig ogsÃ¥ Ã¸ke testflaten
til funksjonene dine.


## Dette var jo interessant, meeeeen...

Du tenker sikkert: "Ã… skrive property-based tester for konseptuelt enkle
sorteringsalgoritmer er Ã©n ting, men hvordan kan jeg skrive tilsvarende tester i
mitt langt mer komplekse og mindre generelle domene?". Det er et godt og
betimelig spÃ¸rsmÃ¥l.

I neste innlegg i denne serien skal vi gjÃ¸re nettopp dÃ©t!
